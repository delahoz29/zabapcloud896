@AccessControl.authorizationCheck: #NOT_ALLOWED
@EndUserText.label: 'Transient - Analytical Query'
@ObjectModel.modelingPattern: #ANALYTICAL_QUERY
@ObjectModel.supportedCapabilities: [#ANALYTICAL_QUERY]
define transient view entity ZI_CDS_31_896
  provider contract analytical_query
  as projection on ZI_CDS_30_896
{
           @AnalyticsDetails.query.axis: #FREE
           SoKey,
           @AnalyticsDetails.query.axis: #ROWS
           LifecycleStatus,

           @AnalyticsDetails.query.axis: #COLUMNS
           QuantitySum,
           UomSum,

           @ObjectModel.text.element: [ 'CurrencyDescription' ]
           CurrencySum,
           _Currency._Text.CurrencyName                                   as CurrencyDescription : localized,

           @Aggregation.default: #FORMULA
           cast('0.05'as  abap.decfloat34 )                               as Discount,

           @Aggregation.default: #FORMULA
           @Semantics.quantity.unitOfMeasure: 'AmPerQuanUnit'
           curr_to_decfloat_amount( AmountSum ) / $projection.quantitysum as AmountPerQuantity,

  virtual  AmPerQuanUnit  : dd_cds_calculated_unit,

           @Aggregation.default: #FORMULA
           @Semantics.amount.currencyCode: 'TargetCurrency'
           currency_conversion( amount => curr_to_decfloat_amount( AmountSum ),
                                source_currency => CurrencySum,
                                target_currency => abap.cuky'EUR',
                                exchange_rate_date => CreatedOn )         as ConvertedAmount,

  virtual  TargetCurrency : abap.cuky,

           //           CreatedAt,
           //           BuyerId,
           //           ShipToId,

           CreatedBy,
           CreatedOn,
           /* Associations */
           _Currency
}
